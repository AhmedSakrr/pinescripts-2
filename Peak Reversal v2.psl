// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Zettt
// 2021

//@version=4
study("Peak Reversal v2", shorttitle="Peak Reversal v2", overlay=true)

showOnlyFirstSignal = true
keltnerEMAlength = input(title="Keltner Channel EMA Length", defval=21, minval=1, type=input.integer)
keltnerMultiplier = input(title="Keltner Bands Normal Multiplier", defval=2.25, minval=1, type=input.float)
maximumPeakMultiplier = input(title="Keltner Extreme Multiplier", defval=3.375, minval=1, type=input.float)
showFreeBars = input(title="Show Free Bars?", type=input.bool, defval=true)

// define Keltner bands 
atrlength = 14
keltnerEMA = ema(close, keltnerEMAlength)
upKeltnerBand = ( keltnerEMA + (rma(tr(true), atrlength) * keltnerMultiplier) )
downKeltnerBand = ( keltnerEMA - (rma(tr(true), atrlength) * keltnerMultiplier) )

upMaximumBand = ( keltnerEMA + (rma(tr(true), atrlength) * maximumPeakMultiplier) )
downMaximumBand = ( keltnerEMA - (rma(tr(true), atrlength) * maximumPeakMultiplier) )

// plot bands
plot(keltnerEMA, title="Keltner Channel EMA (Mean)", color=#BB6083, linewidth=1, transp=40)
plot(upKeltnerBand, title="Normal Upper Band", color=#DD8EAD, linewidth=1, transp=40)
plot(downKeltnerBand, title="Normal Lower Band", color=#DD8EAD, linewidth=1, transp=40)
plot(upMaximumBand, title="Extreme Upper Band", color=#DD8EAD, linewidth=1, transp=50)
plot(downMaximumBand, title="Extreme Lower Band", color=#DD8EAD, linewidth=1, transp=50)

// plot bar colors depending on how many bars are outside the band
// outside the upper band
firstFreeBarUp = high <= upKeltnerBand and close <= upKeltnerBand
numFreeBarsUp = barssince(firstFreeBarUp)

color colorBarUp = na

if numFreeBarsUp > 5
    colorBarUp := #b71c1c
else if numFreeBarsUp > 2
    colorBarUp := #ef5350
else if numFreeBarsUp > 0
    colorBarUp := #ef9a9a
else
    colorBarUp := na
    
barcolor(colorBarUp, title="Up Deviations")
// color=#81c784

// outside lower band
firstFreeBarDown = low >= downKeltnerBand and close >= downKeltnerBand
numFreeBarsDown = barssince(firstFreeBarDown)

color colorBarDown = na

if numFreeBarsDown > 5
    colorBarDown := #1b5e20
else if numFreeBarsDown > 2
    colorBarDown := #66bb6a
else if numFreeBarsDown > 0
    colorBarDown := #a5d6a7
else
    colorBarDown := na
    
barcolor(colorBarDown, title="Down Deviations")

// Plot normal band breakouts
longBreakout = high >= upKeltnerBand // and not low >= upKeltnerBand // this is to prevent overlap, essentially a free bar is also cutting through the normal bands
shortBreakout = close <= downKeltnerBand // and open >= downKeltnerBand

showLongBreakout = showOnlyFirstSignal ? longBreakout and not longBreakout[1] : longBreakout
showShortBreakout = showOnlyFirstSignal ? shortBreakout and not shortBreakout[1] : shortBreakout

plotshape(showLongBreakout, title="Normal Band Cross Up", style=shape.triangledown, size=size.tiny, color=#e57373, transp=60, location=location.abovebar)
plotshape(showShortBreakout, title="Normal Band Cross Down", style=shape.triangleup, size=size.tiny, color=#81c784, transp=60, location=location.belowbar)

// Plot maximum breakouts
longMaximumBreakout = high >= upMaximumBand // and open <= upMaximumBand
shortMaximumBreakout = low <= downMaximumBand // and open >= downMaximumBand

showLongMaximumBreakout = showOnlyFirstSignal ? longMaximumBreakout and not longMaximumBreakout[1] : longMaximumBreakout
showShortMaximumBreakout = showOnlyFirstSignal ? shortMaximumBreakout and not shortMaximumBreakout[1] : shortMaximumBreakout

plotshape(showLongMaximumBreakout, title="Extreme Band Cross Up", style=shape.triangledown, size=size.tiny, color=#e57373, transp=30, location=location.abovebar)//, display=display.none)
plotshape(showShortMaximumBreakout, title="Extreme Band Cross Down", style=shape.triangleup, size=size.tiny, color=#81c784, transp=30, location=location.belowbar)//, display=display.none)

// Plot free bars breakouts
longFreeBar = low >= upKeltnerBand
shortFreeBar = high <= downKeltnerBand

// plotshape(showFreeBars ? longFreeBar : na, title="Free Bar Up", style=shape.triangledown, size=size.tiny, color=#e57373, transp=10, location=location.abovebar)//, display=display.none)
// plotshape(showFreeBars ? shortFreeBar : na, title="Free Bar Down", style=shape.triangleup, size=size.tiny, color=#81c784, transp=10, location=location.belowbar)//, display=display.none)
plotchar(showFreeBars ? longFreeBar : na, title="Free Bar Up", char="★", size=size.tiny, color=#e57373, transp=10, location=location.abovebar)//, display=display.none)
plotchar(showFreeBars ? shortFreeBar : na, title="Free Bar Down", char="★", size=size.tiny, color=#81c784, transp=10, location=location.belowbar)//, display=display.none)
